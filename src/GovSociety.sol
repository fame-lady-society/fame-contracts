// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
//                                         #*==+**
//                                          *::---+#@
//                                          +::--:-+##
//                                         %=::-:::=++#
//                                         #---::::-==+
//                                         *-:-::::==-+%
//                                         =:::::.:=--+#
//                                        %=:-::::-=:-=*
//                                        #-:-::.:--::=+
//                                        *:-::::-=-:--=
//                                       %-:-:::===----=%
//                                       #::::::==-----=
//                                       %#+-::=+=-----+#*---:---=*
//                                       +=++*+===----::::::::-----=#
//                                      @=-:::---=---::::::::------:+#
//                                       =-:..:===----::::=+**-----:-+
//                                       =+==+=:+----::-=*   *-------+%
//                                       #+--:==+-----=*%    *-------+#
//                                        %=-.-=+----=*      *--=----+*
//                                         %++--+=-=+#       *===-===++
//                                            #++**%@        *=======+*
//                                                           *=======+*
//                                                          %*====++=+*
//                                                          %*===+++=+*
//                                                          %+===+++=*#
//                                                          #========*%                 ****#
//                                                          *========*%              #+=----=+##
//                                                          +=======+*             #+----------=*#
//                                                         %========+#           %*=-=#% %=-------+#
//                                                         #========*%          *+-=*%    *--------+*
//                                                         *=======+*%         #+-=*%     #---------+#
//                                                        #========+*         #*==+#      #=--------=#
//                                                       @*========+*        %#==+#       #=---=-----*
//                                                   %##*+--=======+#        #+==+#       *=--===----*#
//                                                   +==#+=========*#       #*===*%       *=======--==#
//                                                    %#%+-========*%      %*===+#        *-====--=+-:*
//                                                      #+=--======*#     %*+===+%       %=--::-=++=-+*
//                                                      #======-----+*%  %*+++==+#      %=:-:-=+==---=+
//                                                      *======-======*##*++++=+#      %=-=======++==*#
//                                                     %+=====--=========++====+@     %#++==-----+#*##
//                                                     *==-===--==--=======--*##      %*++**#+=+###%@
//                                                    #+==-==---==-=======---+++
//                                                    *==-===--=+**+======--=*=+
//                                                   %+--------*%   #*+===--++*%
//                                                  @#+===-:--=@
//                                                  #*==+===-*#
//                                                 %*+=======#
//                                                #*+======+*
//                                               %#+=======*
//                                               #++=====+#%
//                                              #++====+*%
//                                          %#**==+==+*%
//                                         *=:=+==+=*%%
//                                         #+-=+==*#%*+++#
//                                           %#*#%   *+==+#
//                                                   #+===#
//                                                  #*====#
//                                                 %*+====#
//                                                %*+====**
//                                                *+=====*
//                        =*%                    *+===--+*
//                       *+*#                   #+-==-:=*
//                      %%%%%%                 #*+===--+
//                     %%%%%%%                #++#%#*+*@
//                     ##%#**##              #-=+#%%%%
//                    %#*#***##             *--#%%%%%
//                    ++++=+++#            #****#%%%@
//                   #*+++**+#%           *******##@
//                   #****+=+#%         %#*++++**#@
//                   ++=+++==#%        %*++++++*#%
//                   %#*+===*#        #*+++++++%@
//                    ##*+=*%@       #*+++++=+**+==***#%%%%#####*##****###########%
//                       %#%@       #*+++++++++=-::=*######*************##########*==*%
//                              %#***++++++++++==-=***************#*****###########***+*%
//                          %#***+**+=++++++++++==******###############*###########*++=+*%
//                      %%*+=++++++++++=++++*****#%%%%                       %%%%%#*++=+*#%
//                  %*+=+*+=====++++++==+*#%@                    ####%%            @%#*+*##%
//                *=:...:---=--:-----:-=*                        #####*#%        %##*#*+*#*#
//             %*+-===++*+=++=---=***#%@                           %##*##%      %%#####%@%%@
//           ##*++=++++++++####*+*##*#%                           ####****%
//         #*=---=------=*****####%##%                            #######*#
//        *+==--=----=++ *++**####%%%                   %###%
//      *=--=-----=+    ==++#####%%@                    #***##%#%
//     *+-::--=+**     *+==+*####%                      %#*******#%
//    *=====++**      *++=-=*###%                        @%*******##@
//   *======+**     +=-=+=:=##%%                      %#***********#%
//  #======++      **=--+--*#%%                @%##**++++*######%%%%@
// %+======+      +==-=**+**#%             %#*******#%%%         %%#*##
// *=======*     *+++++*++*#             %*****#%%%           %#*****#%
// *======+    **+++==++=*%             %****#%            %##****##%%@%##%
// *======+****+++====++*%              #***#%          %##******###**####%
// %+======+**+++++++++%                %#*********+++**+*********#####%
//  #*====+**++++++***#                   %##****++==++*++**#####%%
//   #*+======----+##                       @%#**+=+**++*##%%@
//     #**++++++**%@                     @##******+*+++****##%%
//         %##%                      %#***********++==++******##%%
//                              %%****+++**********==+********##*#%
//                         %##***++++++++*****              ####*##%
//                        #*+++++++++++++*****#            *####*##%
//                        *++++++++++++++*********+       **#**###%
//                        %#**++*******+++**********+**********###
//                            @%%%% %#*+++***%%#**+==+**++****##%
//                                   #*+++******#*=+*****+****%
//                                   @#*++*********+********#%
//                                     %#********+-=******+*#
//                                       %###********##%%%***#
//                                             %%###      %%**#
//                                                          %*##
//                                                          %#*#%
//                                                           %###%
//                                                           %####
//                                                           %#*##@
//                                                           %**#*
//                                                           %**##
//                                                           #*##%
pragma solidity ^0.8.22;

import "@openzeppelin5/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin5/contracts/utils/cryptography/EIP712.sol";
import "@openzeppelin5/contracts/token/ERC721/extensions/ERC721Votes.sol";
import "@openzeppelin5/contracts/access/AccessControl.sol";
import "@openzeppelin5/contracts/token/ERC721/extensions/ERC721Wrapper.sol";
import {LibBitmap} from "solady/utils/LibBitmap.sol";
import {ITokenURIGenerator} from "./ITokenURIGenerator.sol";
import {IERC4906} from "./IERC4906.sol";

interface ITokemEmitable {
    function emitBatchMetadataUpdate(uint256 start, uint256 end) external;
    function emitMetadataUpdate(uint256 tokenId) external;
}

contract GovSociety is
    ERC721Wrapper,
    EIP712,
    ERC721Votes,
    AccessControl,
    ITokemEmitable,
    IERC4906
{
    using LibBitmap for LibBitmap.Bitmap;
    LibBitmap.Bitmap private _lockedTokensIds;
    mapping(uint256 => address) private _guardianForTokenId;
    bytes32 public constant TOKEN_URI_GENERATOR_ROLE =
        keccak256("TOKEN_URI_GENERATOR_ROLE");
    bytes32 public constant RENDERER_ROLE = keccak256("RENDERER_ROLE");
    ITokenURIGenerator public renderer;

    uint256 private _totalSupply;

    constructor(
        address underlyingToken,
        address defaultAdmin,
        address _renderer
    )
        ERC721Wrapper(IERC721(underlyingToken))
        ERC721("Society Governance", "gSOCIETY")
        EIP712("GovSociety", "1")
    {
        _grantRole(DEFAULT_ADMIN_ROLE, defaultAdmin);
        _grantRole(TOKEN_URI_GENERATOR_ROLE, defaultAdmin);
        _grantRole(RENDERER_ROLE, _renderer);
        renderer = ITokenURIGenerator(_renderer);
    }

    function clock() public view override returns (uint48) {
        return uint48(block.timestamp);
    }

    // solhint-disable-next-line func-name-mixedcase
    function CLOCK_MODE() public pure override returns (string memory) {
        return "mode=timestamp";
    }

    function totalSupply() public view returns (uint256) {
        return _totalSupply;
    }

    modifier onlyTokenOwner(uint256 tokenId) {
        require(
            ownerOf(tokenId) == msg.sender,
            "Only token owner can call this function"
        );
        _;
    }

    error TokenIsAlreadyLocked(uint256 tokenId);

    function lock(uint256 tokenId) public onlyTokenOwner(tokenId) {
        if (isLocked(tokenId)) {
            revert TokenIsAlreadyLocked(tokenId);
        }
        _lockedTokensIds.set(tokenId);
    }

    function lockMany(uint256[] memory tokenIds) public {
        for (uint256 i = 0; i < tokenIds.length; i++) {
            lock(tokenIds[i]);
        }
    }

    error BurnAddressCannotBeGuardian();

    function lockWithGuardian(
        uint256 tokenId,
        address guardian
    ) public onlyTokenOwner(tokenId) {
        if (isLocked(tokenId)) {
            revert TokenIsAlreadyLocked(tokenId);
        }
        if (guardian == address(0)) {
            revert BurnAddressCannotBeGuardian();
        }
        _lockedTokensIds.set(tokenId);
        _guardianForTokenId[tokenId] = guardian;
    }

    function lockWithGuardianMany(
        uint256[] memory tokenIds,
        address guardian
    ) public {
        for (uint256 i = 0; i < tokenIds.length; i++) {
            lockWithGuardian(tokenIds[i], guardian);
        }
    }

    // @dev if guardian exists, caller must be the guardian. otherwise they must be the owner
    error OnlyGuardianOrOwner(uint256 tokenId);
    modifier onlyGuardianOrOwner(uint256 tokenId) {
        address g = guardianForTokenId(tokenId);
        if (
            (g != address(0) && msg.sender != g) ||
            (g == address(0) && msg.sender != ownerOf(tokenId))
        ) {
            revert OnlyGuardianOrOwner(tokenId);
        }
        _;
    }
    error TokenIsNotLocked(uint256 tokenId);
    function unlock(uint256 tokenId) public onlyGuardianOrOwner(tokenId) {
        if (!isLocked(tokenId)) {
            revert TokenIsNotLocked(tokenId);
        }
        _lockedTokensIds.unset(tokenId);
        _guardianForTokenId[tokenId] = address(0);
    }

    function unlockMany(uint256[] memory tokenIds) public {
        for (uint256 i = 0; i < tokenIds.length; i++) {
            unlock(tokenIds[i]);
        }
    }

    function isLocked(uint256 tokenId) public view returns (bool) {
        return _lockedTokensIds.get(tokenId);
    }

    function guardianForTokenId(uint256 tokenId) public view returns (address) {
        return _guardianForTokenId[tokenId];
    }

    function lockedTokenCount(
        uint256 start,
        uint256 amount
    ) public view returns (uint256) {
        return _lockedTokensIds.popCount(start, amount);
    }

    error TokenIsLocked(uint256 tokenId);
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public override {
        // Dissallow transfers of locked tokens
        if (isLocked(tokenId)) {
            revert TokenIsLocked(tokenId);
        }

        super.transferFrom(from, to, tokenId);
    }

    error InvalidRenderer(address renderer);
    function setRenderer(
        address _renderer
    ) public onlyRole(TOKEN_URI_GENERATOR_ROLE) {
        if (_renderer == address(0)) {
            revert InvalidRenderer(address(0));
        }
        _revokeRole(RENDERER_ROLE, address(renderer));
        renderer = ITokenURIGenerator(_renderer);
        _grantRole(RENDERER_ROLE, address(renderer));
    }

    function tokenURI(
        uint256 tokenId
    ) public view override returns (string memory) {
        return renderer.tokenURI(tokenId);
    }

    // The following functions are overrides required by Solidity.
    function _update(
        address to,
        uint256 tokenId,
        address auth
    ) internal override(ERC721, ERC721Votes) returns (address) {
        if (to == address(0)) {
            _totalSupply--;
        } else if (_ownerOf(tokenId) == address(0)) {
            _totalSupply++;
        }

        return super._update(to, tokenId, auth);
    }

    function _increaseBalance(
        address account,
        uint128 value
    ) internal override(ERC721, ERC721Votes) {
        super._increaseBalance(account, value);
    }

    function supportsInterface(
        bytes4 interfaceId
    ) public view override(ERC721, AccessControl) returns (bool) {
        return
            interfaceId == bytes4(0x49064906) || // IERC4906
            super.supportsInterface(interfaceId);
    }

    function emitBatchMetadataUpdate(
        uint256 start,
        uint256 end
    ) public override onlyRole(RENDERER_ROLE) {
        emit BatchMetadataUpdate(start, end);
    }

    function emitMetadataUpdate(
        uint256 tokenId
    ) public override onlyRole(RENDERER_ROLE) {
        emit MetadataUpdate(tokenId);
    }
}
